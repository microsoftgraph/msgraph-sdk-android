apply plugin: 'com.android.library'
apply plugin: 'maven'

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

def getVersionCode() {
    return mavenMajorVersion.toInteger() * 10000 + mavenMinorVersion.toInteger() * 100 + mavenPatchVersion.toInteger()
}

def getVersionName() {
    return "${mavenMajorVersion}.${mavenMinorVersion}.${mavenPatchVersion}"
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode this.getVersionCode()
        versionName this.getVersionName()
    }

    lintOptions {
        disable 'InvalidPackage', 'AllowBackup'
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        buildTypes.each {
            it.buildConfigField 'String', 'CLIENT_ID', ClientId
            it.buildConfigField 'String', 'USERNAME', Username
            it.buildConfigField 'String', 'PASSWORD', Password
        }
    }
    dexOptions {
        javaMaxHeapSize "8g"
    }
}

apply plugin: 'checkstyle'

check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle) {
    configFile file("../checkstyle.xml")
    source 'src'
    include 'main/**/*.java'
    exclude '**/generated/**',
            '**/extensions/**',
            '**/core/GraphErrorCodes.java',
            '**/http/Graph*Error*.java'
    ignoreFailures = false
    classpath = files()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.5'
    androidTestImplementation 'junit:junit:4.12'
}

uploadArchives {
    def bintrayUsername = ""
    def bintrayApikey = ""
    if (project.rootProject.file('local.properties').exists()) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        bintrayUsername = properties.getProperty('bintray.user')
        bintrayApikey = properties.getProperty('bintray.apikey')
    }

    configuration = configurations.archives
    repositories.mavenDeployer {
        pom {
           setGroupId project.mavenGroupId
           setArtifactId project.mavenArtifactId
           setVersion getVersionName()
        }
        repository (url: project.mavenRepoUrl) {
            authentication(
                 // put these values in local file ~/.gradle/gradle.properties
                 userName: project.hasProperty("bintrayUsername") ? project.bintrayUsername : bintrayUsername,
                 password: project.hasProperty("bintrayApikey") ? project.bintrayApikey : bintrayApikey
            )
        }
    }
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath())
    classpath += configurations.compile
    options.encoding = 'UTF-8'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task copyResDirectoryToClasses(type: Copy) {
    from "${projectDir}/src/androidTest/res"
    into "${buildDir}/intermediates/classes/androidTest/debug/res"
}

//assembleDebug.dependsOn(copyResDirectoryToClasses)